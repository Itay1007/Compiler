/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;
import java.io.PrintWriter;


parser code 
{:
	public Lexer lexer;
	private PrintWriter file_writer;

	static public final int PARSING_ERROR = 0;
	static public final int LEXICAL_ERROR = 1;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}

	public Parser(Lexer lexer, PrintWriter file_writer)
	{
		this(lexer); // calls the above constructor.
		this.file_writer = file_writer;
	}

	public void syntax_error(Symbol curr_token)
	{	
		/* We get here when there was a syntax error */

		int err_line  = lexer.getLine();

		report_error("ERROR" + "(" + err_line + ")", PARSING_ERROR);
	}

	public void report_error(String message, Object info)
	{
		if ((int) info == PARSING_ERROR){

			/* error is syntactic (we were called from the 'syntax_error' method) */

			System.out.print("Parsing ");
			
		}
		else{
			/* error is lexical */

			System.out.print("Lexical ");
		}

		System.out.println("Error at " + "[" + lexer.getLine() + ":" + lexer.getTokenStartPosition() + "]");

		file_writer.print(message);
		file_writer.close();

		System.exit(0);
	}

	public int getLine()
	{
		return this.lexer.getLine();
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	try{
		Symbol s;
		s = lexer.next_token();

		System.out.println(TokenNames.terminalNames[s.sym]);

		return s;
	}	
	catch(Throwable e)
	{	
		report_error("ERROR" + "(" + lexer.getLine() + ")", LEXICAL_ERROR);
	}

	// Won't get here
	return null;	
:};

/*******************************/
/* 			TERMINALS          */
/*******************************/

/* --------- Operators --------*/
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal LT;
terminal GT;
terminal ASSIGN;

/* ---------- */

terminal IF;
terminal WHILE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal NIL;
terminal COMMA;
terminal TYPE_INT;
terminal TYPE_VOID;
terminal ARRAY;
terminal CLASS;
terminal EXTENDS;
terminal RETURN;
terminal NEW;
terminal TYPE_STRING;


/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;


/*****************/
/* NON TERMINALS */
/*****************/

non terminal AST_PROGRAM Program;
non terminal AST_DEC_LIST decList; 
non terminal AST_DEC dec;
non terminal AST_VARDEC varDec;
non terminal AST_FUNCDEC funcDec;
non terminal AST_CLASSDEC classDec;
non terminal AST_ARRAYTYPEDEF arrayTypedef;
non terminal AST_TYPE type;
non terminal AST_EXP exp;
non terminal AST_NEWEXP newExp;
non terminal AST_CFIELD cField;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_STMT_LIST stmtList;
non terminal AST_EXP_LIST expList;
non terminal AST_TYPE_LIST typeList;
non terminal AST_STMT stmt;
non terminal AST_VAR var;


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence left EQ;
precedence left LT;
precedence left GT;
precedence left PLUS;
precedence left MINUS;
precedence left TIMES;
precedence left DIVIDE;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence left DOT;

/************************/
/* START WITH: Program */
/************************/
start with Program;

/********************/
/* DERIVATION RULES */
/********************/

Program		::=		decList:l				{: RESULT = new AST_PROGRAM(l, lleft + 1);    :}
					;

decList		::=		dec:d   decList:dl				{: RESULT = new AST_DEC_LIST(d, dl, dleft + 1);    :}
					| dec:d   						{: RESULT = new AST_DEC_LIST(d, null, dleft + 1);    :}
					;

dec			::=		varDec:v				{: RESULT = new AST_DEC(v, 0, vleft + 1);    :}
					| funcDec:f				{: RESULT = new AST_DEC(f, 1, fleft + 1);    :}
					| classDec:c			{: RESULT = new AST_DEC(c, 2, cleft + 1);    :}
					| arrayTypedef:a		{: RESULT = new AST_DEC(a, 3, aleft + 1);    :}
					;

type		::=		TYPE_INT		{: RESULT = new AST_TYPE(0, null, getLine());    :}
					| TYPE_STRING	{: RESULT = new AST_TYPE(1, null, getLine());    :}
					| TYPE_VOID		{: RESULT = new AST_TYPE(2, null, getLine());    :}
					| ID:name		{: RESULT = new AST_TYPE(3, name, nameleft + 1);    :}
					;

arrayTypedef::=		ARRAY ID:name EQ type:t LBRACK RBRACK SEMICOLON		{: RESULT = new AST_ARRAYTYPEDEF(name,t, nameleft + 1);    :}
					;

varDec		::=		type:t ID:name SEMICOLON					{: RESULT = new AST_VARDEC_EXP(t, name, null, nameleft + 1);    :}
					| type:t ID:name ASSIGN exp:e SEMICOLON		{: RESULT = new AST_VARDEC_EXP(t, name, e, tleft + 1);    :}
					| type:t ID:name ASSIGN newExp:e SEMICOLON	{: RESULT = new AST_VARDEC_NEWEXP(t, name, e, tleft + 1);    :}
					;

funcDec		::=		type:t ID:func_name LPAREN RPAREN LBRACE stmtList:body RBRACE				{: RESULT = new AST_FUNCDEC(t, func_name, null, body, func_nameleft + 1);    :}
					|type:t ID:func_name LPAREN typeList:params_types RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_FUNCDEC(t, func_name, params_types, body, func_nameleft + 1);    :}
					;

classDec	::=		CLASS ID:class_name LBRACE cFieldList:fields RBRACE						{: RESULT = new AST_CLASSDEC(class_name, null, fields, class_nameleft + 1);    :}
					| CLASS ID:class_name EXTENDS ID:super_class LBRACE cFieldList:fields RBRACE	{: RESULT = new AST_CLASSDEC(class_name, super_class, fields, class_nameleft + 1);    :}
					;

newExp  	::=		NEW type:t 								{: RESULT = new AST_NEWEXP(t, null, tleft + 1);    :}
					| NEW type:t LBRACK exp:e RBRACK			{: RESULT = new AST_NEWEXP(t, e, tleft + 1);    :}
					;

cField  	::=		varDec:vd 								{: RESULT = new AST_CFIELD_VARDEC(vd, vdleft + 1);    :}
					| funcDec:fd 							{: RESULT = new AST_CFIELD_FUNDEC(fd, fdleft +  1);    :}
					;

cFieldList	::=		cField:c	cFieldList:l		{: RESULT = new AST_CFIELD_LIST(c,l, cleft + 1);    :}
					| cField:c						{: RESULT = new AST_CFIELD_LIST(c,null, cleft + 1); :}
					;

expList		::=		exp:e COMMA	expList:li		{: RESULT = new AST_EXP_LIST(e,li, eleft + 1);    :}
					| exp:e						{: RESULT = new AST_EXP_LIST(e,null, eleft + 1); :}
					;

typeList	::=		type:t ID:name COMMA typeList:tl		{: RESULT = new AST_TYPE_LIST(t, name, tl, nameleft + 1);    :}
					| type:t ID:name						{: RESULT = new AST_TYPE_LIST(t, name, null, nameleft + 1); :}
					;

stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AST_STMT_LIST(s,l, sleft + 1);    :}
					| stmt:s				{: RESULT = new AST_STMT_LIST(s,null, sleft + 1); :}
					;


var			::=		ID:name								{: RESULT = new AST_VAR_SIMPLE(name, nameleft + 1);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AST_VAR_FIELD(v,fieldName, vleft + 1); :} 
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AST_VAR_SUBSCRIPT(v,e, vleft + 1);     :} 
					;
					
stmt		::=		var:v ASSIGN exp:e SEMICOLON								{: RESULT = new AST_STMT_ASSIGN(v,e, vleft + 1);	 :}
					| IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(   cond,body, condleft + 1); 	:}
					| WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(cond,body, condleft + 1); 	:}
					| varDec:vd													{: RESULT = new AST_STMT_VARDEC(vd, vdleft + 1); 	:}
					| var:v ASSIGN newExp:ne SEMICOLON							{: RESULT = new AST_STMT_ASSIGN_NEWEXP(v,ne, vleft +  1); 	:}
					| RETURN SEMICOLON											{: RESULT = new AST_STMT_RETURN(null, getLine() - 1); 		:}
					| RETURN exp:e SEMICOLON									{: RESULT = new AST_STMT_RETURN(e, eleft + 1); :}
					| ID:name LPAREN RPAREN SEMICOLON								{: RESULT = new AST_STMT_FUNCCALL(null, name, null, nameleft + 1); 	:}
					| var:v DOT ID:func_name LPAREN RPAREN SEMICOLON						{: RESULT = new AST_STMT_FUNCCALL(v, func_name, null, vleft + 1); 	:}
					| ID:func_name LPAREN expList:args RPAREN SEMICOLON					{: RESULT = new AST_STMT_FUNCCALL(null, func_name, args, func_nameleft + 1); 	:}
					| var:v DOT ID:func_name LPAREN expList:args RPAREN SEMICOLON			{: RESULT = new AST_STMT_FUNCCALL(v, func_name, args, vleft + 1); 	:}
					;

exp			::=		INT:i								{: RESULT = new AST_EXP_INT(i, 0, ileft + 1);          :}
					| MINUS INT:i						{: RESULT = new AST_EXP_INT(i, 1, ileft + 1);          :}
					| STRING:s							{: RESULT = new AST_EXP_STRING(s, sleft + 1);          :}
					| NIL								{: RESULT = new AST_EXP_NIL(getLine());          :}
					| var:v								{: RESULT = new AST_EXP_VAR(v, vleft + 1);          :}
					| exp:e1 PLUS  exp:e2				{: RESULT = new AST_EXP_BINOP(e1, e2, 0, e1left + 1);		:}
					| exp:e1 MINUS exp:e2				{: RESULT = new AST_EXP_BINOP(e1, e2, 1, e1left + 1);		:}
					| exp:e1 TIMES exp:e2				{: RESULT = new AST_EXP_BINOP(e1, e2, 2, e1left + 1);		:}
					| exp:e1 DIVIDE exp:e2				{: RESULT = new AST_EXP_BINOP(e1, e2, 3, e1left + 1);		:}
					| exp:e1 LT exp:e2					{: RESULT = new AST_EXP_BINOP(e1, e2, 4, e1left + 1);		:}
					| exp:e1 GT exp:e2					{: RESULT = new AST_EXP_BINOP(e1, e2, 5, e1left + 1);		:}
					| exp:e1 EQ exp:e2					{: RESULT = new AST_EXP_BINOP(e1, e2, 6, e1left + 1);		:}
					| LPAREN exp:e RPAREN				{: RESULT = new AST_EXP_PARENEXP(e, eleft + 1);		:}
					| ID:func_name LPAREN RPAREN				{: RESULT = new AST_EXP_FUNCCALL(null, func_name, null, func_nameleft + 1);		:}
					| var:v DOT ID:func_name LPAREN RPAREN		{: RESULT = new AST_EXP_FUNCCALL(v, func_name, null, vleft + 1);		:}
					| ID:func_name LPAREN expList:args RPAREN		{: RESULT = new AST_EXP_FUNCCALL(null, func_name, args, func_nameleft + 1);		:}
					| var:v DOT ID:func_name LPAREN expList:args RPAREN		{: RESULT = new AST_EXP_FUNCCALL(v, func_name, args, vleft + 1);		:}
					;
